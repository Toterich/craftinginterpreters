program        -> statement* EOF;
statement      -> exprStmt | ifStmt | printStmt | varDeclStmt | whileStmt | forStmt | blockStmt | breakStmt ;
exprStmt       -> expression ";" ;
ifStmt         -> "if" "(" expression ")" statement ("else" statement)? ;
printStmt      -> "print" expression ";" ;
varDeclStmt    -> "var" IDENTIFIER ("=" expression)? ";" ;
whileStmt      -> "while" "(" expression ")" statement ;
forStmt        -> "for" "(" (varDeclStmt | exprStmt | ";" )
                   expression? ";"
                   expression? ")" statement ;
breakStmt      -> "break" ";" ;
blockStmt      -> "{" statement* "}" ;
expression     -> comma_op ;
comma_op       -> assignment ("," assignment)* ;
assignment     -> IDENTIFIER "=" assignment | logic_or ;
logic_or       -> logic_and ("or" logic_and)* ;
logic_and      -> equality ("and" equality)* ;
equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           -> factor ( ( "-" | "+" ) factor )* ;
factor         -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) unary
               | primary ;
primary        -> NUMBER | STRING | IDENTIFIER | "true" | "false" | "nil"
               | "(" expression ")" ;