program        -> statement* EOF;
statement      -> exprStmt | printStmt | varDeclStmt | blockStmt;
blockStmt      -> "{" statement* "}";
varDeclStmt    -> "var" IDENTIFIER ("=" expression)? ";";
printStmt      -> "print" expression ";"
exprStmt       -> expression ";"
expression     -> comma_op
comma_op       -> assignment ("," assignment)* ;
assignment     -> IDENTIFIER "=" assignment | equality;
equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           -> factor ( ( "-" | "+" ) factor )* ;
factor         -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) unary
               | primary ;
primary        -> NUMBER | STRING | IDENTIFIER | "true" | "false" | "nil"
               | "(" expression ")" ;